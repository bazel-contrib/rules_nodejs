load("@io_bazel_rules_sass//:defs.bzl", "sass_binary")
load("@npm//@bazel/karma:index.bzl", "karma_web_test_suite")
load("@npm//@bazel/typescript:index.bzl", "ts_library")
load("//tools:angular_ts_library.bzl", "ng_ts_library")

package(default_visibility = ["//:__subpackages__"])

sass_binary(
    name = "hello-world-styles",
    src = "hello-world.component.scss",
)

ng_ts_library(
    name = "hello-world",
    srcs = [
        "hello-world.component.ts",
        "hello-world.module.ts",
    ],
    angular_assets = [
        ":hello-world.component.html",
        ":hello-world-styles",
    ],
    tsconfig = "//src:tsconfig.json",
    deps = [
        "//src/lib/shorten",
        "//src/shared/material",
        "@npm//@angular/core",
        "@npm//@angular/forms",
        "@npm//@angular/router",
        "@npm//date-fns",
    ],
)

ts_library(
    name = "test_lib",
    testonly = 1,
    srcs = glob(["*.spec.ts"]),
    tsconfig = "//src:tsconfig-test",
    deps = [
        ":hello-world",
        "//src/shared/material",
        "@npm//@angular/core",
        "@npm//@angular/platform-browser",
        "@npm//@types/jasmine",
    ],
)

karma_web_test_suite(
    name = "test",
    srcs = [
        # We are manaully adding the bazel generated named-UMD date-fns bundle here as
        # named-UMD bundles for non-APF npm packages are not yet automatically added.
        # This file is generated by the npm_umd_bundle @npm//date-fns:date-fns__umd
        # rule that is setup by yarn_install.
        "@npm//date-fns:date-fns.umd.js",
    ],
    # do not sort
    bootstrap = [
        "@npm//:node_modules/zone.js/dist/zone-testing-bundle.js",
        "@npm//:node_modules/reflect-metadata/Reflect.js",
    ],
    browsers = [
        "@io_bazel_rules_webtesting//browsers:chromium-local",
        "@io_bazel_rules_webtesting//browsers:firefox-local",
    ],
    tags = ["native"],
    runtime_deps = [
        "//src:initialize_testbed",
    ],
    deps = [
        ":test_lib",
        "//src:rxjs_umd_modules",
        # explicit tslib dependency comes from @angular/material
        "@npm//:node_modules/tslib/tslib.js",
    ],
)
