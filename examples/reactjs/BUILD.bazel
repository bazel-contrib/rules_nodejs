load("@build_bazel_rules_nodejs//:defs.bzl", "http_server", "rollup_bundle")
load("@build_bazel_rules_nodejs//internal/web_package:web_package.bzl", "web_package")
load("@npm_bazel_jasmine//:index.bzl", "jasmine_node_test")
load("@npm_bazel_typescript//:index.bzl", "ts_devserver", "ts_library")

package(default_visibility = ["//visibility:public"])

ts_library(
    name = "app",
    srcs = glob([
        "*.ts",
        "*.tsx",
    ]),
    deps = [
        "@npm//@types/react",
        "@npm//@types/react-dom",
    ],
)

ts_devserver(
    name = "devserver",
    entry_module = "examples_reactjs/app",
    index_html = ":index.html",
    static_files = [
        ":index.html",
    ],
    # We'll collect all the devmode JS sources from these TypeScript libraries
    deps = [
        ":app",
        "@npm//react:react__umd",
        "@npm//react-dom:react-dom__umd",
    ],
)

rollup_bundle(
    name = "bundle",
    enable_code_splitting = False,
    entry_point = ":app.prod.ts",
    deps = [
        ":app",
        "@npm//react",
        "@npm//react-dom",
    ],
)

web_package(
    name = "package",
    assets = [
        # For differential loading, we supply both an ESModule entry point and an es5 entry point
        # The injector will put two complimentary script tags in the index.html
        ":bundle.min.js",
        ":bundle.min.es2015.js",
    ],
    index_html = "index.html",
)

http_server(
    name = "prodserver",
    data = [":package"],
    templated_args = ["package"],
)

# Just a dummy test so that we have a test target for //... on certain bazelci platforms with bazel_integration_test
jasmine_node_test(
    name = "prodmode_test",
    srcs = ["prodmode.spec.js"],
    data = [
        "bundle.es2015.js",
        "index.html",
        "@npm//domino",
    ],
    templated_args = ["--node_options=--experimental-modules"],
)

# For testing from the root workspace of this repository with bazel_integration_test.
filegroup(
    name = "all_files",
    srcs = glob(
        ["**/*"],
        exclude = [
            "bazel-out/**/*",
            "dist/**/*",
            "node_modules/**/*",
        ],
    ),
    visibility = ["//visibility:public"],
)
