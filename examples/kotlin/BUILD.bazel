# Add rules here to build your software
# See https://docs.bazel.build/versions/master/build-ref.html#BUILD_files

load("@build_bazel_rules_nodejs//internal/web_package:web_package.bzl", "web_package")
load("@io_bazel_rules_kotlin//kotlin:kotlin.bzl", "kt_js_import", "kt_js_library")
load("@npm//http-server:index.bzl", "http_server")
load("@npm_bazel_jasmine//:index.bzl", "jasmine_node_test")
load("@npm_bazel_rollup//:index.bzl", "rollup_bundle")
load("@npm_bazel_terser//:index.bzl", "terser_minified")

# Grab a Maven dependency
kt_js_import(
    name = "kotlinx-html-js",
    jars = ["@maven//:v1/http/dl.bintray.com/kotlin/kotlinx.html/org/jetbrains/kotlinx/kotlinx-html-js/0.6.12/kotlinx-html-js-0.6.12.jar"],
    srcjar = "@maven//:v1/http/dl.bintray.com/kotlin/kotlinx.html/org/jetbrains/kotlinx/kotlinx-html-js/0.6.12/kotlinx-html-js-0.6.12-sources.jar",
)

# This will create hello.js
kt_js_library(
    name = "hello",
    srcs = [":HelloWorld.kt"],
    deps = [":kotlinx-html-js"],
)

# Copy boostrap.js to the bin folder as _bootstrap.js
# so that relative import to `./hello.js` is valid
genrule(
    name = "bootstrap",
    srcs = ["bootstrap.js"],
    outs = ["_bootstrap.js"],
    cmd = "cp $< $@",
)

rollup_bundle(
    name = "bundle",
    srcs = ["hello.js"],
    config_file = "rollup.config.js",
    entry_point = "_bootstrap.js",
    # TODO: make this example work with format = "esm"
    format = "cjs",
    output_dir = True,
    deps = [
        "@npm//kotlin",
        "@npm//kotlinx-html-js",
    ],
)

terser_minified(
    name = "bundle.min",
    src = ":bundle",
)

web_package(
    name = "package",
    data = [":bundle.min"],
    index_html = "index.html",
)

# TODO: fix prodserver; require.js is needed if rollup_bundle output is cjs
http_server(
    name = "server",
    data = [":package"],
    templated_args = ["package"],
)

jasmine_node_test(
    name = "test",
    srcs = ["spec.js"],
    data = [
        ":bundle",
        "@npm//domino",
    ],
    templated_args = ["--node_options=--experimental-modules"],
)

# For testing from the root workspace of this repository with bazel_integration_test.
filegroup(
    name = "all_files",
    srcs = glob(
        include = ["**/*"],
        exclude = [
            "bazel-out/**/*",
            "dist/**/*",
            "node_modules/**/*",
        ],
    ),
    visibility = ["//visibility:public"],
)
