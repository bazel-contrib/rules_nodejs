load("@build_bazel_rules_nodejs//:defs.bzl", "jasmine_node_test")

# Test what happens when we depend on the catch-all "node_modules" rule rather
# than declare our dependencies on individual npm packages.
# This is the legacy behavior prior to 0.13, so it also proves backwards-compatibility.
[jasmine_node_test(
    name = "test_%s" % pkgmgr,
    srcs = [
        "coarse.spec.js",
        "common.spec.js",
    ],
    node_modules = "@fine_grained_deps_%s//:node_modules" % pkgmgr,
) for pkgmgr in [
    "yarn",
    "npm",
]]

# Test what happens when only certain NPM packages are in our dependencies.
# These packages and their dependencies are copied to the execroot, but
# the rest are not.
[jasmine_node_test(
    name = "fine_grained_test_%s" % pkgmgr,
    srcs = [
        "common.spec.js",
        "fine.spec.js",
    ],
    deps = [
        "@fine_grained_deps_%s//ajv" % pkgmgr,
        "@fine_grained_deps_%s//jasmine" % pkgmgr,
        "@fine_grained_deps_%s//typescript" % pkgmgr,
        "@fine_grained_deps_%s//rxjs" % pkgmgr,
        # Note, test-b depends on test-a@0.0.1 which should get
        # hoisted to node_modules/test-b/node_modules/test-a
        "@fine_grained_deps_%s//@gregmagolan/test-b" % pkgmgr,
    ],
) for pkgmgr in [
    "yarn",
    "npm",
]]
