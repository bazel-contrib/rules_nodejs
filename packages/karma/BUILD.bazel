# Copyright 2019 The Bazel Authors. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_binary", "nodejs_test")
load("@build_bazel_rules_nodejs//:tools/defaults.bzl", "pkg_npm")
load("@build_bazel_rules_nodejs//packages/typescript:index.bzl", "ts_library")
load("@build_bazel_rules_nodejs//tools/stardoc:index.bzl", "stardoc")
load("//third_party/github.com/bazelbuild/bazel-skylib:rules/copy_file.bzl", "copy_file")

package(default_visibility = ["//visibility:public"])

ts_library(
    name = "bazel_karma_lib",
    srcs = glob(["*.ts"]),
    module_name = "@bazel/karma",
    tsconfig = ":tsconfig.json",
    deps = [
        "@npm//@types/node",
        "@npm//tmp",
    ],
)

nodejs_binary(
    name = "karma_bin",
    data = [
        ":bazel_karma_lib",
        "@npm//jasmine-core",
        "@npm//karma",
        "@npm//karma-chrome-launcher",
        "@npm//karma-firefox-launcher",
        "@npm//karma-jasmine",
        "@npm//karma-requirejs",
        "@npm//karma-sourcemap-loader",
        "@npm//requirejs",
        "@npm//tmp",
    ],
    entry_point = "@npm//:node_modules/karma/bin/karma",
    install_source_map_support = False,
)

bzl_library(
    name = "bzl",
    testonly = True,
    srcs = glob(["*.bzl"]),
    deps = [
        "@build_bazel_rules_nodejs//:bzl",
        "@build_bazel_rules_nodejs//internal/js_library:bzl",
        "@io_bazel_rules_webtesting//web",
    ],
)

stardoc(
    name = "docs",
    testonly = True,
    out = "index.md",
    input = "index.bzl",
    tags = ["fix-windows"],
    deps = [":bzl"],
)

nodejs_test(
    name = "test",
    data = [
        "docs_test.js",
        "index.md",
    ],
    entry_point = ":docs_test.js",
    tags = [
        "fix-windows",
    ],
)

# Ugly genrule depending on local linux environment to build the README out of skylark doc generation.
# Only referenced when we do a release.
# TODO: This ought to be possible with stardoc alone. Need to coordinate with Chris Parsons.
genrule(
    name = "generate_README",
    srcs = [
        "index.md",
        "install.md",
    ],
    outs = ["README.md"],
    cmd = """cat $(execpath install.md) $(execpath index.md) | sed 's/^##/\\\n##/' > $@""",
    tags = ["fix-windows"],
    visibility = ["//docs:__pkg__"],
)

copy_file(
    name = "npm_version_check",
    src = "//internal:npm_version_check.js",
    out = ":npm_version_check.js",
)

pkg_npm(
    name = "npm_package",
    package_name = "@bazel/karma",
    srcs = [
        "index.bzl",
        "karma.conf.js",
        "karma_web_test.bzl",
        "package.bzl",
        "package.json",
    ],
    build_file_content = """exports_files(["karma.conf.js"])""",
    substitutions = {
        "#@external ": "",
        "//packages/karma:karma_bin": "@npm//karma/bin:karma",
    },
    deps = [
        "//packages/karma:bazel_karma_lib",
        ":npm_version_check",
    ] + select({
        # FIXME: fix stardoc on Windows; //packages/karma:index.md generation fails with:
        #   ERROR: D:/b/62unjjin/external/npm_bazel_karma/BUILD.bazel:65:1: Couldn't build file
        #   external/npm_bazel_karma/docs.raw: Generating proto for Starlark doc for docs failed (Exit 1)
        "@bazel_tools//src/conditions:windows": [],
        "//conditions:default": [":generate_README"],
    }),
)
