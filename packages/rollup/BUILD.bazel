# Copyright 2017 The Bazel Authors. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@build_bazel_rules_nodejs//:tools/defaults.bzl", "codeowners", "pkg_npm")
load("//third_party/github.com/bazelbuild/bazel-skylib:rules/copy_file.bzl", "copy_file")

codeowners(
    teams = ["@jbedard"],
)

# Ugly genrule depending on local linux environment to build the README out of skylark doc generation.
# Only referenced when we do a release.
# TODO: This ought to be possible with stardoc alone. Need to coordinate with Chris Parsons.
genrule(
    name = "generate_README",
    srcs = [
        "@npm_bazel_rollup//:index.md",
        "//packages/rollup/docs:install.md",
    ],
    outs = ["README.md"],
    cmd = """cat $(execpath //packages/rollup/docs:install.md) $(execpath @npm_bazel_rollup//:index.md) | sed 's/^##/\\\n##/' > $@""",
    tags = ["fix-windows"],
    visibility = ["//docs:__pkg__"],
)

# Copy the proto file to a matching third_party/... nested directory
# so the runtime require() statements still work
_worker_proto_dir = "third_party/github.com/bazelbuild/bazel/src/main/protobuf"

genrule(
    name = "copy_worker_js",
    srcs = ["//packages/worker:npm_package"],
    outs = ["worker.js"],
    cmd = "cp $(execpath //packages/worker:npm_package)/index.js $@",
    visibility = ["//visibility:public"],
)

copy_file(
    name = "copy_worker_proto",
    src = "@build_bazel_rules_typescript//%s:worker_protocol.proto" % _worker_proto_dir,
    out = "%s/worker_protocol.proto" % _worker_proto_dir,
    visibility = ["//visibility:public"],
)

pkg_npm(
    name = "npm_package",
    srcs = [
        "@npm_bazel_rollup//:package_contents",
    ],
    build_file_content = """exports_files(["rollup.config.js"])""",
    substitutions = {
        "//:rollup-worker-local": "@npm//@bazel/rollup/bin:rollup-worker",
    },
    vendor_external = [
        "npm_bazel_rollup",
    ],
    deps = select({
        # FIXME: fix stardoc on Windows; @npm_bazel_rollup//:index.md generation fails with:
        #   ERROR: D:/b/62unjjin/external/npm_bazel_rollup/BUILD.bazel:32:1: Couldn't build file
        #   external/npm_bazel_rollup/docs.raw: Generating proto for Starlark doc for docs failed (Exit 1)
        "@bazel_tools//src/conditions:windows": [],
        "//conditions:default": [":generate_README"],
    }) + [
        ":copy_worker_proto",
        ":worker.js",
    ],
)
