load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_binary")
load("//packages/concatjs:index.bzl", "ts_library")
load("//packages/concatjs/test/some_module:run_node_test.bzl", "ts_write_file")

# We compile this library with the module_name and package_name "sm" to make it possible to use
# `import {} from 'sm';` both at type-check time (we include the module_name mapping in the paths
# map in tsconfig) as well as runtime (we link the module tonode_modules/package_name at runtime).
ts_library(
    name = "some_module",
    package_name = "sm",
    srcs = ["index.ts"],
    module_name = "sm",
)

ts_library(
    name = "main",
    srcs = ["main.ts"],
    deps = [":some_module"],
)

nodejs_binary(
    name = "bin",
    data = [
        ":main",
        ":some_module",
    ],
    entry_point = ":main.ts",
)

sh_test(
    name = "module_load_test",
    srcs = ["module_load_test.sh"],
    data = [
        ":bin",
        "@build_bazel_rules_nodejs//third_party/github.com/bazelbuild/bazel/tools/bash/runfiles",
    ],
)

# ts_library for compiling the tool
ts_library(
    name = "writer",
    srcs = ["writer.ts"],
    deps = ["@npm//@types/node"],
)

# nodejs_binary for generating an executable for the tool :writer
nodejs_binary(
    name = "writer_bin",
    data = [
        ":writer",
    ],
    entry_point = ":writer.ts",
)

# custom rule using the executable above via run_node
# invoked via bazel build //packages/concatjs/test/some_module:test
# as --nobazel_patch_module_resolver is the default, the entry_point on the executable is resolved
# to the ts file - writer.ts
# this works for bazel run //packages/concatjs/test/some_module:writer_bin as this will patch require and resolve the
# ts to js
ts_write_file(
    name = "test",
    content = "hello world",
)
