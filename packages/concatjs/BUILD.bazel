# Copyright 2017 The Bazel Authors. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("replacements.bzl", "TYPESCRIPT_REPLACEMENTS")
load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@build_bazel_rules_nodejs//:index.bzl", "js_library")
load("@build_bazel_rules_nodejs//:tools/defaults.bzl", "pkg_npm")
load("@build_bazel_rules_nodejs//packages/typescript:index.bzl", "ts_project")
load("@build_bazel_rules_nodejs//tools/stardoc:index.bzl", "stardoc")
load("//third_party/github.com/bazelbuild/bazel-skylib:rules/copy_file.bzl", "copy_file")

ts_project(
    name = "bazel_concatjs_lib",
    srcs = glob(["*.ts"]),
    tsconfig = {
        "compilerOptions": {
            "declaration": True,
            "module": "umd",
            "moduleResolution": "node",
            "types": ["node"],
        },
    },
    deps = [
        "@npm//@types/node",
        "@npm//karma",
    ],
)

js_library(
    name = "bazel_concatjs",
    package_name = "@bazel/concatjs",
    srcs = ["bazel_concatjs_lib"],
    visibility = ["//packages/concatjs:__subpackages__"],
)

bzl_library(
    name = "bzl",
    testonly = True,
    srcs = glob(["*.bzl"]) + [
        # Work-around since we don't have and don't wnat a bzl_library in the generated
        # @nodejs//:BUILD.bazel file
        "@nodejs//:index.bzl",
    ],
    deps = [
        "//packages/concatjs/devserver:bzl",
        "//packages/concatjs/internal:bzl",
        "//packages/concatjs/web_test:bzl",
        "@build_bazel_rules_nodejs//:bzl",
        "@build_bazel_rules_nodejs//internal/common:bzl",
        "@build_bazel_rules_nodejs//internal/node:bzl",
    ],
)

stardoc(
    name = "docs",
    testonly = True,
    out = "README.md_",
    input = "index.docs.bzl",
    tags = ["fix-windows"],
    deps = [":bzl"],
)

genrule(
    name = "docs_scrub_platform",
    srcs = ["README.md_"],
    outs = ["README.md"],
    cmd = "sed -e 's#devserver:devserver_.*_a[mr][dm]64#devserver:devserver_[platform]#' <$< >$@",
    visibility = ["//docs:__pkg__"],
)

copy_file(
    name = "npm_version_check",
    src = "//internal:npm_version_check.js",
    out = ":npm_version_check.js",
)

pkg_npm(
    name = "npm_package",
    package_name = "@bazel/concatjs",
    srcs = [
        "index.bzl",
        "package.json",
        "//packages/concatjs/devserver:package_contents",
        "//packages/concatjs/internal:package_contents",
        "//packages/concatjs/third_party/github.com/bazelbuild/bazel/src/main/protobuf:npm_package_assets",
        "//packages/concatjs/third_party/npm/requirejs:package_contents",
        "//packages/concatjs/web_test:package_contents",
    ],
    build_file_content = "",
    substitutions = TYPESCRIPT_REPLACEMENTS,
    deps = [
        ":README.md",
        ":bazel_concatjs_lib",
        ":npm_version_check",
        "//packages/concatjs/devserver:devserver-darwin",
        "//packages/concatjs/devserver:devserver-linux",
        "//packages/concatjs/devserver:devserver-windows",
        "//packages/concatjs/internal:BUILD",
        "//packages/concatjs/internal:generated_BUILD",
        "//packages/concatjs/internal:tsc_wrapped",
    ],
)
