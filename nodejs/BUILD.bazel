load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("//nodejs/private:stamp.bzl", "stamp_setting")
load("//nodejs/private:toolchains_repo.bzl", "PLATFORMS")
load("//nodejs/private:user_build_settings.bzl", "user_args")

filegroup(
    name = "package_contents",
    srcs = glob(["*"]) + [
        "//nodejs/private:package_contents",
    ],
    visibility = ["//:__pkg__"],
)

bzl_library(
    name = "bzl",
    srcs = glob(["*.bzl"]) + ["@bazel_tools//tools:bzl_srcs"],
    visibility = ["//visibility:public"],
    deps = [
        "//nodejs/private:bzl",
        "//nodejs/private/providers:bzl",
        "//third_party/github.com/bazelbuild/bazel-skylib:bzl",
    ],
)

# This is the target rule authors should put in their "toolchains"
# attribute in order to get a node interpreter for the correct
# platform.
# See https://docs.bazel.build/versions/main/toolchains.html#writing-rules-that-use-toolchains
toolchain_type(
    name = "toolchain_type",
    visibility = ["//visibility:public"],
)

[
    platform(
        name = key,
        constraint_values = values.compatible_with,
    )
    for key, values in PLATFORMS.items()
]

# User set flags that are passed to the build
# you need to make sure that you pass the correct repo name infront for this flag to work
# if you are using rules from build_bazel_rules_nodejs that should be the @ in the flag
# ex: bazel build --@build_bazel_rules_nodejs//nodejs:default_args="test" main
# use rules_nodejs when you use things from that repo
# bazel test --//nodejs:default_args="" //...
user_args(
    name = "default_args",
    build_setting_default = "--preserve-symlinks",
    visibility = ["//visibility:public"],
)

# Detect if the build is running under --stamp
config_setting(
    name = "stamp",
    values = {"stamp": "true"},
    visibility = ["//visibility:public"],
)

# Enable stamping based on the --stamp flag
stamp_setting(
    name = "use_stamp_flag",
    stamp = select({
        "//nodejs:stamp": True,
        "//conditions:default": False,
    }),
    visibility = ["//visibility:public"],
)

stamp_setting(
    name = "always_stamp",
    stamp = True,
    visibility = ["//visibility:public"],
)

stamp_setting(
    name = "never_stamp",
    stamp = False,
    visibility = ["//visibility:public"],
)
